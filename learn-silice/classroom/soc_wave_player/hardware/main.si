// SL 2022-01-10 @sylefeb
$$if Solution_HEADER then
//
// Learning Silice
//
// Building a simple but feature complete SOC, step by step.
//
// Meant to be used on a ULX3S board with SSD1351 SPIscreen
// See README.md for instructions and exercises
//
$$end
// https://github.com/sylefeb/Silice
// MIT license, see LICENSE_MIT in Silice repo root

// Pre-compilation script, embeds compiled code within a string
// - Code has to be compiled into firmware/code.hex before
$$dofile('pre_include_compiled.lua')

// Setup memory
// - we allocate 2^(addrW-1) uint32
// - the topmost bit is used to indicate peripheral access
$$addrW      = 15
$$memmap_bit = addrW-1

// Configure BRAM (needed for write mask)
$$config['bram_wmask_byte_wenable_width'] = 'data'

// Includes the processor
$include('../../../projects/ice-v/CPUs/ice-v.si')

$$if Solution_SCREEN then
// Includes the SPIscreen driver
$include('../../../projects/ice-v/SOCs/ice-v-oled.si')
$$end

$$if Solution_UART then
// Includes the UART controller
$$uart_in_clock_freq_mhz = 25
$$uart_bauds             = 115200
$include('../../../projects/common/uart.si')
$$end

$$if Solution_STREAM then
// --------------------------------------------------
// Audio output
// --------------------------------------------------

unit audio(input  uint8 audio_in,output uint4 audio_out)
{
$$if not Solution_PWM then
  always {
    // simple passthrough
    audio_out = audio_in[4,4];
  }
$$else
  uint8 counter(0);
  always {
    uint4 low  = 4b0;
    uint4 high = 4b1;
    uint8 frac = audio_in[0,8];
    audio_out  = counter < frac ? low : high;
    counter    = counter + 1;
  }
$$end
}
$$end

// --------------------------------------------------
// SOC
// --------------------------------------------------

// Memory interface between SOC and CPU
group bram_io
{
  uint4       wenable(0), // write enable mask (xxxx, 0:nop, 1:write)
  uint32      wdata(0),   // data to write
  uint32      rdata(0),   // data read from memory
  uint$addrW$ addr(0),    // address, init is boot address
}

// The SOC starts here
// - some input/outputs do not exist in simulation and
//   are therefore enclosed in pre-processor conditions
unit main(
  output uint8 leds,
$$if BUTTONS then
  input  uint7 btns,
$$end
$$if AUDIO then
  output uint4 audio_l,
  output uint4 audio_r,
$$end
$$if Solution_SCREEN then
  output uint1 oled_clk,
  output uint1 oled_mosi,
  output uint1 oled_dc,
  output uint1 oled_resn,
  output uint1 oled_csn(0),
$$if VERILATOR then
  // configuration for SPIscreen simulation
  output uint2  spiscreen_driver(1/*SSD1351*/),
  output uint10 spiscreen_width(128),
  output uint10 spiscreen_height(128),
$$end
$$end
$$if Solution_SPIflash then
$$if SPIFLASH then
  output uint1 sf_clk,
  output uint1 sf_csn,
  output uint1 sf_mosi,
  input  uint1 sf_miso,
$$end
$$end
$$if Solution_UART then
$$if UART then
  output uint1 uart_tx,
  input  uint1 uart_rx,
$$end
$$end
$$if Solution_SD then
$$if SDCARD then
  output  uint1  sd_clk,
  output  uint1  sd_csn,
  output  uint1  sd_mosi,
  input   uint1  sd_miso,
$$end
$$end
) {

$$if Solution_SCREEN then
  // SPIscreen (OLED) controller chip
  uint1 displ_dta_or_cmd <: prev_wdata[10,1];
  uint8 displ_byte       <: prev_wdata[0,8];
  uint1 internal_oled_mosi(0);
  uint1 internal_oled_clk(0);
  uint1 internal_oled_dc(0);
  oled display(
    data_or_command <: displ_dta_or_cmd,
    byte            <: displ_byte,
    oled_din        :> oled_mosi,
    oled_clk        :> oled_clk,
    oled_dc         :> oled_dc,
  );
$$end

$$if Solution_SPIflash then
  // spiflash miso register
  // -> avoid readin a pin directly in design as it is asynchronous
  //    we always track it in a register which updates on clock posedge
  uint1       reg_sf_miso(0);
$$if not SPIFLASH then
  // for simulation ('fake' inputs/outputs)
  uint1       sf_clk(0);
  uint1       sf_csn(0);
  uint1       sf_mosi(0);
  uint1       sf_miso(0);
$$end
$$end

$$if Solution_SD then
  // SDCARD miso register
  uint1        reg_sd_miso(0);
$$if not SDCARD then
  // for simulation ('fake' inputs/outputs)
  uint1  sd_clk(0);
  uint1  sd_csn(0);
  uint1  sd_mosi(0);
  uint1  sd_miso(0);
$$end
$$end

$$if Solution_UART then
  // UART transmit interface
  uart_out uo;
$$if UART then
  // UART transmit chip
  uart_sender usend<reginputs>(
    io      <:> uo,
    uart_tx :>  uart_tx
  );
$$end
$$end

$$if Solution_BTN then
  // buttons inputs register
  // -> also an asynchronous signal, so we register it
  uint7 reg_btns(0);
$$if not BUTTONS then
  uint7 btns(0);
$$end
$$end

$$if Solution_AUDIO then
$$if not AUDIO then
  // audio bypass for simulation
  uint4 audio_l(0);
  uint4 audio_r(0);
$$end
$$end

$$if Solution_STREAM then
  // audio output
  audio audio;
  // audio streaming
  $$PERIOD = 3124
  // we allocated 2x 512 8bit samples
  simple_dualport_bram uint8 audio_buffer[1024] = {pad(0)};
  uint1  audio_buffer_select(0); // buffer to which we write
  uint10 audio_buffer_sample(0); // sample being played
  uint12 audio_counter(0);
  uint10 audio_buffer_start_waddr <:: audio_buffer_select ? 512 : 0;
  uint10 audio_buffer_start_raddr <:: audio_buffer_select ?   0 : 512;
  uint32 audio_addr_cpu           <:: 32h18000 | {22b0,audio_buffer_start_waddr};
$$end

	// for peripherals memory mapping, record previous cycle CPU access
	uint$addrW$ prev_mem_addr(0);
	uint4       prev_mem_rw(0);
  uint32      prev_wdata(0);

$$if SIMULATION then
   // count cycles in simulation for debugging purposes
   uint32 cycle(0);
$$end

  // RAM
  // Instantiate the memory interface
  bram_io memio;
  // Instantiate a BRAM holding the system's RAM
  // -> uses template "bram_wmask_byte", that turns wenable into a byte mask
  bram uint32 mem<"bram_wmask_byte">[$1<<(addrW-1)$] = $meminit$;

  // Instantiate our CPU!
  rv32i_cpu cpu( mem <:> memio );

  // --- SOC logic, the always block is always active
  always {
    // track whether the CPU is reading a memory mapped peripheral
    uint1 memmap_r     =  prev_mem_addr[$memmap_bit$,1] & (prev_mem_rw == 4b0);
    uint1 is_device    = ~prev_mem_addr[$memmap_bit-1$,1];
$$if Solution_AUDIO then
    uint1 audio_access =  prev_mem_addr[$memmap_bit-1$,1];
$$end
    // track memory mapped peripherals access
$$if Solution_LED then
    uint1 leds_access            = prev_mem_addr[ 0,1] & is_device;
$$end
$$if Solution_SCREEN then
    uint1 display_en_access      = prev_mem_addr[ 1,1] & is_device;
    uint1 display_reset_access   = prev_mem_addr[ 2,1] & is_device;
$$end
$$if Solution_UART then
    uint1 uart_access            = prev_mem_addr[ 3,1] & is_device;
$$end
$$if Solution_SPIflash then
    uint1 sf_access              = prev_mem_addr[ 4,1] & is_device;
$$end
$$if Solution_SD then
    uint1 sd_access              = prev_mem_addr[ 5,1] & is_device;
$$end
$$if Solution_BTN then
    uint1 button_access          = prev_mem_addr[ 6,1] & is_device;
$$end
	  // ---- memory access
    // BRAM wenable update following wenable from CPU <-> SOC interface
    mem.wenable = memio.wenable & {4{~memio.addr[$memmap_bit$,1]}};
		//                            ^^^^^^^ no BRAM write if in peripheral addresses
    // data sent to the CPU
    memio.rdata   = // read data is either from memory or SOC peripherals
       (~memmap_r                  ? mem.rdata          : 32b0)
$$if Solution_SPIflash then
     | ((memmap_r & sf_access)     ? {31b0,reg_sf_miso} : 32b0)
$$end
$$if Solution_SD then
     | ((memmap_r & sd_access)     ? {31b0,reg_sd_miso} : 32b0)
$$end
$$if Solution_BTN then
     | ((memmap_r & button_access) ? {31b0,   reg_btns} : 32b0)
$$end
$$if Solution_STREAM then
     | ((memmap_r & audio_access)  ? audio_addr_cpu     : 32b0)
$$end
    ;
    mem.wdata        = memio.wdata;
    mem.addr         = memio.addr;
		// ---- peripherals
$$if Solution_SCREEN then
    display.enable     = 0;       // maintain display enable low (pulses on use)
$$end
$$if Solution_UART then
    uo.data_in_ready   = 0;       // maintain uart trigger low (pulses on use)
$$end
$$if Solution_SPIflash then
    reg_sf_miso        = sf_miso; // register flash miso
$$end
$$if Solution_SD then
    reg_sd_miso        = sd_miso; // register sdcard miso
$$end
$$if Solution_BTN then
    reg_btns           = btns;    // register buttons
$$end
$$if Solution_STREAM then
    audio_l            = audio.audio_out; // feed output to on-board DAC
    audio_r            = audio.audio_out;
    audio.audio_in      = audio_buffer.rdata0; // feed sample to audio out
    audio_buffer_select = audio_buffer_sample[9,1]    // if == 512, done reading
        ? ~audio_buffer_select : audio_buffer_select; // swap buffers
    audio_buffer.addr0  = audio_buffer_start_raddr         // start addr
                        | {1b0,audio_buffer_sample[0,9]};  // current sample
    audio_buffer.wenable1 = 0; // maintain write port low (pulses on CPU write)
    audio_buffer_sample   =
          audio_buffer_sample[9,1] ? 0 : ( // reset counter if done
              audio_counter != $PERIOD$
            ? audio_buffer_sample     // stay on sample if delay not elapsed
            : (audio_buffer_sample+1) // go to next sample
          );
    audio_counter       = (audio_counter == $PERIOD$) ? 0 : (audio_counter+1);
$$end
    // ---- memory mapping to peripherals: writes
    if (prev_mem_addr[$memmap_bit$,1] & (prev_mem_rw != 4b0)) {
$$if Solution_LED then
      /// LEDs
      leds           = leds_access ? prev_wdata[0,8] : leds;
$$end
$$if Solution_SCREEN then
      /// display
      // -> whether to send command or data
      display.enable = display_en_access & (prev_wdata[9,1] | prev_wdata[10,1]);
      // -> SPIscreen reset
      oled_resn      = ~ (display_reset_access & prev_wdata[0,1]);
$$end
$$if Solution_UART then
      /// uart
      uo.data_in_ready = ~uo.busy & uart_access;
      uo.data_in       =  uo.data_in_ready ? prev_wdata[ 0,8] : uo.data_in;
$$end
$$if Solution_SPIflash then
      /// SPIflash output pins
			sf_clk  = sf_access ? prev_wdata[0,1] : sf_clk;
			sf_mosi = sf_access ? prev_wdata[1,1] : sf_mosi;
			sf_csn  = sf_access ? prev_wdata[2,1] : sf_csn;
$$end
$$if Solution_SD then
      /// sdcard output pins
      sd_clk  = sd_access ? prev_wdata[0,1] : sd_clk;
      sd_mosi = sd_access ? prev_wdata[1,1] : sd_mosi;
      sd_csn  = sd_access ? prev_wdata[2,1] : sd_csn;
$$end
$$if Solution_AUDIO then
      /// audio
  $$if not Solution_STREAM then
      if (audio_access) {audio_l = prev_wdata[0,8]; audio_r = prev_wdata[0,8];}
  $$else
      { // block for locally declaring 'which' below
        uint2 which = prev_mem_rw[1,2] | {2{prev_mem_rw[3,1]}};
        //    ^^^^^ produces 0,1,2,3 based on write mask
        audio_buffer.wdata1   = prev_wdata >> {which,3b0};
        //       ^^^ sample to be written ^^ (shift due to 32bits addressing)
        audio_buffer.addr1    = audio_buffer_start_waddr
                              | {prev_mem_addr[0,7],2b0}//addr from CPU (32bits)
                              | which; // sample address
      }
      audio_buffer.wenable1 = audio_access; // write sample
  $$end
$$end

$$if SIMULATION then
      // Simulation debug output, very convenient during development!
$$if Solution_LED then
      if (leds_access) {
        __display("[cycle %d] LEDs: %b (%d)",cycle,leds,prev_wdata);
        if (leds == 255) { __finish(); }// special LED value stops simulation
                                        // convenient to interrupt from firmware
      }
$$end
$$if Solution_UART then
      if (uart_access) { // printf via UART
        __write("%c",prev_wdata[0,8]);
      }
$$end
$$if Solution_SPIflash then
      if (sf_access) {
        __display("[cycle %d] SPI write %b",cycle,prev_wdata[0,3]);
      }
$$end
$$if Solution_AUDIO then
  $$if Solution_STREAM then
      if (audio_access) {
        __display("[cycle %d] audio sample %x, written @%x wenable:%b",
            cycle,audio_buffer.wdata1,audio_buffer.addr1,prev_mem_rw);
      }
  $$else
      if (audio_access) {
        __display("[cycle %d] audio sample %x",cycle,audio_l);
      }
  $$end
$$end
$$end
    }

    // record current access for next cycle memory mapping checks
		prev_mem_addr = memio.addr;
		prev_mem_rw   = memio.wenable;
    prev_wdata    = memio.wdata;

$$if SIMULATION then
    cycle = cycle + 1;
$$end

  } // end of always block

}

// --------------------------------------------------

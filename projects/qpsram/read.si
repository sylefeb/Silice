// @sylefeb 2020
// https://github.com/sylefeb/Silice
// MIT license, see LICENSE_MIT in Silice repo root

$include('../common/qpsram2x.si')

$$if ICESTICK then
import('../common/plls/icestick_25.v')
$$elseif ICE40 then
import('../common/plls/icebrkr_25.v')
$$end

$$uart_bauds = 115200
$$uart_in_clock_freq_mhz = 25
$include('../common/uart.si')

unit main(
  output uint3 leds,
  input  uint1 uart_rx,
  output uint1 uart_tx,
  inout  uint1 ram_io0,
  inout  uint1 ram_io1,
  inout  uint1 ram_io2,
  inout  uint1 ram_io3,
  output uint1 ram_clk,
  output uint1 ram_csn,
)
// clocking (pll) and reset
$$if ICE40 then
<@clock_pll> {
  uint1 clock_pll = uninitialized;
  pll pllgen(
    clock_in  <: clock,
    clock_out :> clock_pll,
  );
$$else
{
$$end

  // QPSRAM RAM
  qpsram_ram ram(
    ram_csn  :> ram_csn,
    ram_clk  :> ram_clk,
    ram_io0 <:> ram_io0,
    ram_io1 <:> ram_io1,
    ram_io2 <:> ram_io2,
    ram_io3 <:> ram_io3,
  );

  // UART receiver
  uart_in ui;
  uart_receiver urecv(io <:> ui,uart_rx <: uart_rx);

  // UART sender
  uart_out uo;
  uart_sender usend(
    io      <:> uo,
    uart_tx :>  uart_tx
  );

  uint1  enabled(0);
  uint1  restart(0);
  uint24 addr(0);
  uint3  get_addr(3b111);
  uint32 blip(0);

  always_before {
    uo.data_in_ready = 0; // maintain low, pulse high to send
    ram.in_ready     = 0; // maintain low, pulse high to read/write
    ram.wenable      = 0; // we read
  }

  algorithm {

    uint16 n(0);

    while (1) {
      // wait for read address
      while (get_addr[0,1]) { }
      // read some
      n = 0;
      ram.wenable  = 0;
      ram.addr     = addr;
      while (~n[8,1]) {
        // read from psram
        ram.in_ready = 1;
        while (ram.busy) {}
        // send over uart
        uo.data_in       = ram.rdata;
        uo.data_in_ready = 1;
        while (uo.busy) {}
        // next
        ram.addr = ram.addr + 1;
        n        = n + 1;
      }
      restart = 1;
    }
  }

  always_after {
    leds         = blip;
    blip         = blip >> 1;
    // receiving
    if (ui.data_out_ready) {
      // read address if enabled
      addr         = get_addr[0,1] ? {addr[0,16],ui.data_out} : addr;
      get_addr     = enabled ? get_addr>>1 : get_addr;
      blip         = enabled ? 32hffffffff : 0;
      // enable on tag
      enabled      = (enabled | (ui.data_out == 8hAA));
    } else {
      if (restart) {
        enabled  = 0;
        get_addr = 3b111;
        restart  = 0;
      }
    }
  }

}

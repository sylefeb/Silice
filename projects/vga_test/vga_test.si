// -------------------------
// MIT license, see LICENSE_MIT in Silice repo root
// https://github.com/sylefeb/Silice
// @sylefeb 2019

$$VGA_800_600   = nil
$$VGA_1024_768  = nil
$$VGA_1920_1080 = nil

$$if MOJO then
// Clock
import('../common/plls/mojo_100_25.v')
$$end

$$if ICESTICK then
// Clock
import('../common/plls/icestick_25.v')
$$end

$$if ICEBREAKER then
// Clock
import('../common/plls/icebrkr_25.v')
$$end

$$if RIEGEL then
// Clock
import('../common/plls/riegel_25.v')
$$end

$$if DE10NANO then
// Clock
$$if VGA_1920_1080 then
import('../common/plls/de10nano_173.v')
$$else
import('../common/plls/de10nano_25.v')
$$end
$$end

$$if DE2 then
// Clock
import('../common/plls/de2_25_100_100ph180.v')
$$end

$$if ULX3S then
// Clock
import('../common/plls/ulx3s_100_25.v')
$$end

$$if ECPIX5 then
// Select resolution
$$VGA_800_600 = 1
// Clock
$$if VGA_1024_768 then
import('../common/plls/ecpix5_65.v')
$$elseif VGA_1920_1080 then
import('../common/plls/ecpix5_173.v')
$$elseif VGA_800_600 then
import('../common/plls/ecpix5_36.v')
$$else
import('../common/plls/ecpix5_25.v')
$$end
$$end

// VGA driver
$include('../common/vga.si')

$$if HARDWARE then
// Reset
$include('../common/clean_reset.si')
$$end

// -------------------------

$$if SIMULATION then
algorithm pll(
  output  uint1 video_clock,
  output  uint1 video_reset,
) <autorun>
{
  uint3 counter = 0;
  uint8 trigger = 8b11111111;

  video_clock   := counter[1,1]; // x4 slower
  video_reset   := (trigger > 0);

  always {
    counter = counter + 1;
	  trigger = trigger >> 1;
  }
}
$$end

// -------------------------

algorithm frame_display(
  input   uint10 pix_x,
  input   uint10 pix_y,
  input   uint1  pix_active,
  input   uint1  pix_vblank,
  output! uint$color_depth$ pix_red,
  output! uint$color_depth$ pix_green,
  output! uint$color_depth$ pix_blue
) <autorun> {
  // by default r,g,b are set to zero
  pix_red   := 0;
  pix_green := 0;
  pix_blue  := 0;
  // ---------- show time!
  while (1) {
	  // display frame
	  while (pix_vblank == 0) {
      if (pix_active) {
        pix_blue  = {pix_y[3,$color_depth-3$],3b000};
        pix_green = 0;
        pix_red   = {pix_x[3,$color_depth-3$],3b000};
      }
    }
    while (pix_vblank == 1) {} // wait for sync
  }
}

// -------------------------

algorithm main(
  output! uint$NUM_LEDS$ leds,
$$if VGA then
  // VGA
$$if SIMULATION or DE2 then
  output  uint1 video_clock,
$$end
  output! uint$color_depth$ video_r,
  output! uint$color_depth$ video_g,
  output! uint$color_depth$ video_b,
  output  uint1 video_hs,
  output  uint1 video_vs
$$end
)
$$if not ULX3S and not BARE then
<@video_clock,!video_reset>
$$end
{
  uint1 video_reset = 0;

$$if HARDWARE then
$$if not DE2 then
  uint1 video_clock = 0;
$$end
$$if MOJO then
  uint1 sdram_clock = 0;
  // --- clock
  pll clk_gen (
    CLK_IN1  <: clock,
    CLK_OUT1 :> sdram_clock,
    CLK_OUT2 :> video_clock
  );
$$elseif ICESTICK then
  // --- clock
  uint1 pll_lock = 0;
  pll clk_gen(
    clock_in  <: clock,
    clock_out :> video_clock,
    lock      :> pll_lock
  );
$$elseif ICEBREAKER then
  // --- clock
  pll clk_gen (
    clock_in  <: clock,
    clock_out :> video_clock
  );
$$elseif RIEGEL then
  // --- clock
  pll clk_gen (
    clock_in  <: clock,
    clock_out :> video_clock
  );
$$elseif DE10NANO then
  // --- clock
  uint1 pll_lock = 0;
  pll clk_gen(
    refclk    <: clock,
    outclk_0  :> video_clock,
    locked    :> pll_lock,
    rst       <: reset
  );
$$elseif DE2 then
  // --- clock
  uint1 pll_lock = 0;
  pll clk_gen(
    inclk0    <: clock,
    c0        :> video_clock,
    locked    :> pll_lock,
  );
$$elseif ULX3S then
  // --- clock
  uint1 sdram_clock = 0;
  uint1 pll_lock = 0;
  pll clk_gen(
    clkin    <: clock,
    clkout0  :> sdram_clock,
    clkout1  :> video_clock,
    locked   :> pll_lock
  );
$$elseif ECPIX5 then
  // --- clock
  uint1 pll_lock = 0;
  pll clk_gen(
    clkin    <: clock,
    clkout0  :> video_clock,
    locked   :> pll_lock
  );
$$end
  // --- video reset
  clean_reset vga_rstcond<@video_clock,!reset> (
    out :> video_reset
  );
$$elseif not BARE then
  // --- simulation pll
  pll clockgen<@clock,!reset>(
    video_clock   :> video_clock,
    video_reset   :> video_reset,
  );
$$end

  uint1  active = 0;
  uint1  vblank = 0;
  uint10 pix_x  = 0;
  uint10 pix_y  = 0;

  vga vga_driver (
    vga_hs :> video_hs,
	  vga_vs :> video_vs,
	  active :> active,
	  vblank :> vblank,
	  vga_x  :> pix_x,
	  vga_y  :> pix_y
  );

  frame_display display (
	  pix_x      <: pix_x,
	  pix_y      <: pix_y,
	  pix_active <: active,
	  pix_vblank <: vblank,
	  pix_red    :> video_r,
	  pix_green  :> video_g,
	  pix_blue   :> video_b
  );

  uint8 frame  = 0;

$$if MOJO then
  // unused pins
  spi_miso := 1bz;
  avr_rx := 1bz;
  spi_channel := 4bzzzz;
$$end

$$if ICARUS then
  // we count a number of frames and stop
  while (frame < 8) {
$$else
  // forever
  while (1) {
$$end

    leds = frame;

    while (vblank == 1) { }
	  __display("vblank off");
    while (vblank == 0) { }
    __display("vblank on");
    frame = frame + 1;

  }
}
